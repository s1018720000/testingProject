plugins {
    id 'java'
    id 'org.springframework.boot' version '2.5.3' apply false
    id 'io.spring.dependency-management' version '1.0.11.RELEASE' apply false
}

subprojects {
    group 'com.as'
    version '1.0.0-SNAPSHOT'

    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    sourceCompatibility = targetJavaVersion
    targetCompatibility = targetJavaVersion

    [compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

    configurations {
        all*.exclude group: 'commons-logging'
        developmentOnly
        runtimeClasspath {
            extendsFrom developmentOnly
        }
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    ext {
        shiro_version = "1.7.1"
        thymeleaf_extras_shiro_version = "2.0.0"
        mybatis_boot_version = "2.4.1"
        druid_version = "1.2.6"
        bitwalker_version = "1.21"
        kaptcha_version = "2.3.2"
        swagger_version = "3.0.0"
        pagehelper_boot_version = "1.3.0"
        fastjson_version = "1.2.76"
        oshi_version = "5.7.5"
        jna_version = "5.8.0"
        commons_io_version = "2.10.0"
        commons_fileupload_version = "1.4"
        poi_version = "4.1.2"
        velocity_version = "1.7"
    }

    dependencies {
        implementation platform('org.springframework.boot:spring-boot-dependencies')

        constraints {
            implementation("org.springframework.boot:spring-boot-dependencies:2.2.12.RELEASE") {
                force = true
            }
        }

        implementation "org.springframework.boot:spring-boot-starter"

        implementation "com.alibaba:druid-spring-boot-starter:${druid_version}"
        implementation "com.github.penggle:kaptcha:${kaptcha_version}"
        implementation "org.apache.shiro:shiro-core:${shiro_version}"
        implementation "org.apache.shiro:shiro-spring:${shiro_version}"
        implementation "org.apache.shiro:shiro-ehcache:${shiro_version}"
        implementation "com.github.theborakompanioni:thymeleaf-extras-shiro:${thymeleaf_extras_shiro_version}"
        implementation "eu.bitwalker:UserAgentUtils:${bitwalker_version}"
        implementation "com.github.pagehelper:pagehelper-spring-boot-starter:${pagehelper_boot_version}"
        implementation "com.github.oshi:oshi-core:${oshi_version}"
        implementation "net.java.dev.jna:jna"
        implementation "net.java.dev.jna:jna-platform"
        implementation "commons-io:commons-io:${commons_io_version}"
        implementation "commons-fileupload:commons-fileupload:${commons_fileupload_version}"
        implementation "org.apache.poi:poi-ooxml:${poi_version}"
        implementation "org.apache.velocity:velocity:${velocity_version}"
        implementation "com.alibaba:fastjson:${fastjson_version}"

        implementation("io.springfox:springfox-boot-starter:${swagger_version}") {
            exclude group: "io.swagger", module: "swagger-models"
        }

        implementation "org.apache.commons:commons-lang3"

        compileOnly "org.projectlombok:lombok:1.18.16"
        annotationProcessor "org.projectlombok:lombok:1.18.16"

        //compile project(':as-common')
        //compile project(':as-generator')
        //compile project(':as-framework')
        //compile project(':as-quartz')
        //compile project(':as-system')
    }

    sourceSets {
        main {
            java {
                srcDir 'src/main/java'
            }
            resources {
                srcDir 'src/main/resources'
            }
        }
    }

    jar {
        manifest {
            attributes("provider": "Gradle")
        }
    }
}

allprojects {
//    apply plugin: 'org.springframework.boot'
//    apply plugin: 'io.spring.dependency-management'

    repositories {
        mavenLocal()
        maven { url "http://maven.aliyun.com/nexus/content/groups/public"}
        mavenCentral()
    }
    defaultTasks 'clean', 'build', 'assemble'
}




